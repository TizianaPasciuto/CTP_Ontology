# CQ12
# Which types of cultural heritage are present in the thematic path X (eg, Novum corpus ...)?  
# Quali tipologie di beni culturali sono presenti in un percorso tematico X?  

# P.S. Uncomment row 26 and replace the ThematicPathTitle to be searched in column 21.

PREFIX ctp: <https://w3id.org/ctp#>  
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>  
SELECT (GROUP_CONCAT(distinct concat(?ThematicPathTitle,' (',lang(?ThematicPathTitle), ')') ;separator=" | ") as ?GroupedThematicPathTitles)  ?ThematicPath
    (GROUP_CONCAT(distinct concat(?CulturalResourceTitle,' (',lang(?CulturalResourceTitle), ')') ;separator=" | ") as ?GroupedCulturalResourceTitle)  (GROUP_CONCAT(distinct ?CulturalResource ;separator=" | ") as ?CulturalResourceURI)
    (GROUP_CONCAT(distinct concat(?CulturalResourceType  ,' (',lang(?CulturalResourceType), ')') ;separator=" | ") as ?GroupedCulturalResourceType)  (GROUP_CONCAT(distinct ?Type  ;separator=" | ") as ?CulturalResourceTypeURI)
WHERE{  
  {  
    ?ThematicPath ctp:hasPart ?CulturalResource.  
  }  
  UNION  
  {  
    ?ThematicPath ctp:hasSlot ?OrderedListSlot.  
    ?OrderedListSlot ctp:hasItem ?CulturalResource.  
  }  
  ?CulturalResource ctp:hasType ?Type.  
  Optional{ ?ThematicPath ctp:hasTitle ?ThematicPathTitle}.  
  Optional{ ?CulturalResource ctp:hasTitle ?CulturalResourceTitle}.  
  Optional{ ?Type rdfs:label ?CulturalResourceType}.  
  ?ThematicPath ctp:hasTitle ?p.  
 # FILTER(REGEX(?p, "InsertThematicPathTitleHere", "i")).   
}  group by ?ThematicPath ?CulturalResource
order by ?ThematicPath ?CulturalResource
LIMIT 100  